WHITESPACE = _{ " "+ }
sep = _{ ";" }

fp = @{ int ~ ("." ~ ASCII_DIGIT+)? }
uint = @{ ASCII_DIGIT+ }
int  = @{ "-"? ~ ASCII_DIGIT+ }
string = @{ ASCII_ALPHANUMERIC+ }

triplet_sep = _{ WHITESPACE ~ "|" ~ WHITESPACE }
triplet_fp3 = _{ fp ~ WHITESPACE ~ fp ~ WHITESPACE ~ fp }

f3x3_args_sep = _{ triplet_fp3 ~ triplet_sep ~ triplet_fp3 ~ triplet_sep ~ triplet_fp3 }
f3x3_args_no_sep = _{ triplet_fp3 ~ WHITESPACE ~ triplet_fp3 ~ WHITESPACE ~ triplet_fp3 }

blur = ${ ^"blur" ~ WHITESPACE ~ uint}
brighten = ${ ^"brighten" ~ WHITESPACE ~ int }
contrast = ${ ^"contrast" ~ WHITESPACE ~ fp }
crop = ${ ^"crop" ~ WHITESPACE ~ uint ~ WHITESPACE ~ uint ~ WHITESPACE ~ uint ~ WHITESPACE ~ uint }
filter3x3 = ${ ^"filter3x3" ~ WHITESPACE ~ (f3x3_args_sep | f3x3_args_no_sep) }
flip_horizontal = { ^"fliph" }
flip_vertical = { ^"flipv"  }
grayscale = { ^"grayscale" }
huerotate = ${ ^"huerotate" ~ WHITESPACE ~ int }
invert = { ^"invert" }
resize = ${ ^"resize" ~ WHITESPACE ~ uint ~ WHITESPACE ~ uint }
rotate90 = { ^"rotate90" }
rotate180 = { ^"rotate180" }
rotate270 = { ^"rotate270" }
unsharpen = ${ ^"unsharpen" ~ WHITESPACE ~ fp ~ WHITESPACE ~ int }

env_resize_sampling_filter_name = _{^"resize" ~ WHITESPACE ~ ^"sampling_filter"}
env_resize_preserve_aspect_ratio_name = _{^"resize" ~ WHITESPACE ~ ^"preserve_aspect_ratio"}

set_resize_sampling_filter = ${env_resize_sampling_filter_name ~ WHITESPACE ~ string }
set_resize_preserve_aspect_ratio = ${ env_resize_preserve_aspect_ratio_name }

setenv_available = _{
	set_resize_sampling_filter
    | set_resize_preserve_aspect_ratio
}

setopt = ${^"set" ~ WHITESPACE ~ setenv_available}

operation = _{
    blur
    | brighten
    | contrast
    | crop
    | filter3x3
    | flip_horizontal
    | flip_vertical
    | grayscale
    | huerotate
    | invert
    | resize
    | rotate90
    | rotate180
    | rotate270
    | unsharpen
}

sequence = _{
	(operation | setopt) ~ ( (NEWLINE* ~ EOI) | (sep ~ NEWLINE+) | sep)
}

main = _{ SOI ~ sequence* ~ EOI }
